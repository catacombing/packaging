pkgname=catacomb-alarm
_pkgname=alarm
pkgdesc="Alarm clock client"
pkgver=1.1.0
pkgrel=1
arch=("x86_64" "aarch64")
url="https://github.com/catacombing/alarm"
license=("GPL3")
makedepends=("git" "rust" "cargo" "gtk4" "alsa-lib")
depends=("gtk4")
source=("git+$url.git#tag=v$pkgver?signed")
validpgpkeys=(
    "4DAA67A9EA8B91FCC15B699C85CDAE3C164BA7B4"  # Christian DÃ¼rr <contact@christianduerr.com>
)
sha256sums=("4643e63b0c9c4430b41d74a259b1ea2d8fa1c8a2fb907fb401955a277fe1b045")
replaces=("catacomb-alarm-git")

prepare() {
  cd "$_pkgname"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$_pkgname"
  CARGO_INCREMENTAL=0 cargo build --release --locked --offline --manifest-path rezz/Cargo.toml
  CARGO_INCREMENTAL=0 cargo build --release --locked --offline --manifest-path gtk/Cargo.toml
  CARGO_INCREMENTAL=0 cargo build --release --locked --offline --manifest-path cli/Cargo.toml
}

check() {
  cd "$_pkgname"
  CARGO_INCREMENTAL=0 cargo test --locked --offline --manifest-path rezz/Cargo.toml
  CARGO_INCREMENTAL=0 cargo test --locked --offline --manifest-path gtk/Cargo.toml
  CARGO_INCREMENTAL=0 cargo test --locked --offline --manifest-path cli/Cargo.toml
}

package() {
  cd "$_pkgname"
  desktop-file-install -m 644 --dir "$pkgdir/usr/share/applications/" "gtk/Alarm.desktop"
  install -D -m644 "rezz/org.catacombing.rezz.conf" "$pkgdir/usr/share/dbus-1/system.d/org.catacombing.rezz.conf"
  install -D -m644 "rezz/rezz.service" "$pkgdir/usr/lib/systemd/system/rezz.service"
  install -D -m755 "target/release/rezz" "$pkgdir/usr/bin/rezz"
  install -D -m755 "target/release/alarm-gtk" "$pkgdir/usr/bin/alarm-gtk"
  install -D -m755 "target/release/alarm-cli" "$pkgdir/usr/bin/alarm-cli"
  install -D -m644 "logo.svg" "$pkgdir/usr/share/pixmaps/Alarm.svg"
}
