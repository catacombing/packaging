# Maintainer: Raihan Ahamed (raihan2000) <raihan1999ahamed@gmail.com>
_mode=host
_crossdirect=false
pkgname="hexagonrpcd"
pkgdesc="Qualcomm HexagonFS daemon"
pkgver=0.4.0
pkgrel=1
arch=(
    "x86_64"
    "armv7h"
    "aarch64"
)
license=("GPL-3.0-or-later")
url="https://github.com/linux-msm/hexagonrpc"
depends=("glibc")
makedepends=("linux-headers" "meson" "git")
source=(
    "git+$url.git#tag=v$pkgver"
    "10-fastrpc.rules"
    "hexagonrpcd-adsp-rootpd.service"
    "hexagonrpcd-adsp-sensorspd.service"
    "hexagonrpcd-sdsp.service"
)
sha256sums=(
    "c66a674501110e99a0f4b5c4be16805d4236347f888660c49b902cae004668b3"
    "41dfc4e8c4fd88f461a5a6e4a4e86849eb302f8bcb2b5ce2efa9690f5415d52d"
    "c89cb83fc7fecb232f530bcd60a3dd2d6846426f7c90173a1e078c6e15dc0bc0"
    "c69215694bedbc366fb9aebc8c6c08f27bb612753626f1083b4d30fcefd2fa04"
    "132aff498f306dd1951ae00aaae20b3b8df57d0c6bc92cb579de0e127cac73c4"
)

build() {
    arch-meson hexagonrpc build
    meson compile -C build
}

package() {
    DESTDIR="$pkgdir" meson install --no-rebuild -C build

    echo -e 'g fastrpc\nu fastrpc - "Qualcomm HexagonFS service" /var/lib/fastrpc' |
      install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/fastrpc.conf"

    # Allow access for FastRPC node for FastRPC user/group
    install -Dm 644 10-fastrpc.rules -t "$pkgdir/usr/lib/udev/rules.d/"

    install -Dm755 hexagonrpcd-adsp-rootpd.service "$pkgdir/usr/lib/systemd/system/$pkgname-adsp-rootpd.service"
    install -Dm755 hexagonrpcd-adsp-sensorspd.service "$pkgdir/usr/lib/systemd/system/$pkgname-adsp-sensorspd.service"
    install -Dm755 hexagonrpcd-sdsp.service "$pkgdir/usr/lib/systemd/system/$pkgname-sdsp.service"
}
