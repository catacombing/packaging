pkgname=kumo-git
_pkgname=kumo
pkgdesc="A Wayland Mobile Browser"
pkgver=r44.3e2ef10
pkgrel=1
arch=("x86_64" "aarch64")
url="https://github.com/catacombing/kumo"
license=("GPL3")
makedepends=("git" "rust" "cargo" "wpewebkit-kumo")
depends=("wpewebkit-kumo" "gst-plugins-good" "gst-libav")
source=("git+$url.git")
#source=("git+$url.git#tag=v$pkgver?signed")
#validpgpkeys=("4DAA67A9EA8B91FCC15B699C85CDAE3C164BA7B4")  # Christian DÃ¼rr <contact@christianduerr.com>
sha256sums=("SKIP")

pkgver() {
  cd "$_pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$_pkgname"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$_pkgname"
  CARGO_INCREMENTAL=0 cargo build --release --locked --offline
}

check() {
  cd "$_pkgname"
  CARGO_INCREMENTAL=0 cargo test --locked --offline
}

package() {
  cd "$_pkgname"
  desktop-file-install -m 644 --dir "$pkgdir/usr/share/applications/" "Kumo.desktop"
  install -D -m755 "target/release/$_pkgname" "$pkgdir/usr/bin/$_pkgname"
  install -D -m644 "logo.svg" "$pkgdir/usr/share/pixmaps/Kumo.svg"
}
