pkgname=kumo
pkgdesc="A Wayland Mobile Browser"
pkgver=1.6.2
pkgrel=2
arch=("x86_64" "aarch64")
url="https://github.com/catacombing/kumo"
license=("GPL3")
makedepends=("git" "rust" "cargo" "wpewebkit-kumo")
depends=("wpewebkit-kumo" "gst-plugins-base" "gst-plugins-good" "gst-plugins-bad" "gst-libav" "ttf-font")
source=("git+$url.git#tag=v$pkgver?signed")
validpgpkeys=(
    "4DAA67A9EA8B91FCC15B699C85CDAE3C164BA7B4"  # Christian DÃ¼rr <contact@christianduerr.com>
)
sha256sums=("77b8fdf5d0c950905e8aa0fb599c25e7e2f38ae38f899a0f3872e0cd120d44d3")
replaces=("kumo-git")

prepare() {
  cd "$pkgname"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"
  CARGO_INCREMENTAL=0 cargo build --release --locked --offline
}

check() {
  cd "$pkgname"
  CARGO_INCREMENTAL=0 cargo test --locked --offline
}

package() {
  cd "$pkgname"
  desktop-file-install -m 644 --dir "$pkgdir/usr/share/applications/" "Kumo.desktop"
  install -D -m755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -D -m644 "logo.svg" "$pkgdir/usr/share/pixmaps/Kumo.svg"
}
