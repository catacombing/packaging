pkgname=tremor-git
_pkgname=tremor
pkgdesc="Force-feedback device control utility"
pkgver=r4.e2c33f8
pkgrel=1
arch=("x86_64" "aarch64")
url="https://github.com/catacombing/tremor"
license=("GPL3")
makedepends=("git" "rust" "cargo")
source=("git+$url.git")
#source=("git+$url.git#tag=v$pkgver?signed")
#validpgpkeys=("4DAA67A9EA8B91FCC15B699C85CDAE3C164BA7B4")  # Christian DÃ¼rr <contact@christianduerr.com>
sha256sums=("SKIP")

pkgver() {
  cd "$_pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$_pkgname"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$_pkgname"
  CARGO_INCREMENTAL=0 cargo build --release --locked --offline
}

check() {
  cd "$_pkgname"
  CARGO_INCREMENTAL=0 cargo test --locked --offline
}

package() {
  cd "$_pkgname"
  install -D -m755 "target/release/$_pkgname" "$pkgdir/usr/bin/$_pkgname"
}
