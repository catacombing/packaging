pkgname=tremor
pkgdesc="Force-feedback device control utility"
pkgver=1.0.1
pkgrel=1
arch=("x86_64" "aarch64")
url="https://github.com/catacombing/tremor"
license=("GPL3")
makedepends=("git" "rust" "cargo")
source=("git+$url.git#tag=v$pkgver?signed")
validpgpkeys=(
    "4DAA67A9EA8B91FCC15B699C85CDAE3C164BA7B4"  # Christian DÃ¼rr <contact@christianduerr.com>
)
sha256sums=("2ef0770a9652c5e73dc551e73f1c79f725940d0151dbd276b520237b0389e9bc")
replaces=("tremor-git")

prepare() {
  cd "$pkgname"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"
  CARGO_INCREMENTAL=0 cargo build --release --locked --offline
}

check() {
  cd "$pkgname"
  CARGO_INCREMENTAL=0 cargo test --locked --offline
}

package() {
  cd "$pkgname"
  install -D -m755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
}
