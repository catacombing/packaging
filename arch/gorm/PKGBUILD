pkgname=gorm
pkgdesc="Mobile-optimized Linux WiFi settings"
pkgver=0.1.0
pkgrel=1
arch=("x86_64" "aarch64")
url="https://github.com/catacombing/gorm"
license=("GPL3")
makedepends=("git" "rust" "cargo")
source=("git+$url.git#tag=v$pkgver?signed")
validpgpkeys=(
    "4DAA67A9EA8B91FCC15B699C85CDAE3C164BA7B4"  # Christian DÃ¼rr <contact@christianduerr.com>
)
sha256sums=(
    "3171d8b37c0243451c4d8c7ba65536dafca1238d4ad0293fc50daec2bb1a5334"
)

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"

  CARGO_INCREMENTAL=0 cargo build --release --locked --offline
}

check() {
  cd "$pkgname"

  CARGO_INCREMENTAL=0 cargo test --locked --offline
}

package() {
  cd "$pkgname"
  desktop-file-install -m 644 --dir "$pkgdir/usr/share/applications/" "gorm.desktop"
  install -D -m755 "./target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -D -m644 "logo.svg" "$pkgdir/usr/share/pixmaps/Gorm.svg"
}
