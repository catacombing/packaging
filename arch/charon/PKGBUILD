pkgname=charon
pkgdesc="Wayland Mobile Maps & Navigation"
pkgver=1.4.0
pkgrel=3
arch=("x86_64" "aarch64")
url="https://github.com/catacombing/charon"
license=("GPL3")
makedepends=("git" "rust" "cargo" "boost")
depends=("kyotocabinet" "marisa" "sqlite3" "protobuf")
source=(
    "git+$url.git#tag=v$pkgver?signed"
    "git+https://github.com/chrisduerr/geocoder-nlp.git#commit=56cbd6692a70855909580ab3166bfa23df41aa54"
    "git+https://github.com/openvenues/libpostal.git#commit=25099c506612b34b23b1bfe286ca6321fcf06f35"
    "git+https://github.com/rinigus/sqlite3pp.git#commit=5cf99ae80873d78b2c52296f51fda2f0d8b11e0d"
)
validpgpkeys=(
    "4DAA67A9EA8B91FCC15B699C85CDAE3C164BA7B4"  # Christian DÃ¼rr <contact@christianduerr.com>
)
sha256sums=(
    "0f4ab23f00a1b794082b9c441c4d619581bb6567f7cfa4759fe09ff6970cc611"
    "958820d901049f38277d96fcef92aa61ebc0fd9061f5002ee8b1060eb02fab64"
    "1f6b0dc07d0d9ff8ab5e47f46f5179fd4d81633c6f70c964c415029c903cd9c4"
    "e659b085ed2b30068be938ea446693504c7476101f1f0eec85d4870eab189b4f"
)
options=(!lto)

prepare() {
  cd "$pkgname"

  git submodule init
  git config submodule.geocoder_nlp_rs/geocoder-nlp.url "$srcdir/geocoder-nlp"
  git config submodule.geocoder_nlp_rs/libpostal.url "$srcdir/libpostal"
  git -c protocol.file.allow=always submodule update

  cd "geocoder_nlp_rs/geocoder-nlp"
  git config submodule.thirdparty/sqlite3pp.url "$srcdir/sqlite3pp"
  git -c protocol.file.allow=always submodule update

  cd "../.."

  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"

  CARGO_INCREMENTAL=0 cargo build --release --locked --offline
}

check() {
  cd "$pkgname"

  CARGO_INCREMENTAL=0 cargo test --locked --offline
}

package() {
  cd "$pkgname"
  desktop-file-install -m 644 --dir "$pkgdir/usr/share/applications/" "charon.desktop"
  mkdir -p -m 750 "$pkgdir/usr/share/polkit-1/rules.d"
  install -D -m 644 "./rules/66-charon.rules.polkit" "$pkgdir/usr/share/polkit-1/rules.d/66-charon.rules"
  install -D -m755 "./target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -D -m644 "logo.svg" "$pkgdir/usr/share/pixmaps/Charon.svg"
}
