pkgname=charon
pkgdesc="Wayland Mobile Maps & Navigation"
pkgver=0.2.0
pkgrel=1
arch=("x86_64" "aarch64")
url="https://github.com/catacombing/charon"
license=("GPL3")
makedepends=("git" "rust" "cargo" "boost")
depends=("kyotocabinet" "marisa" "sqlite3")
source=("git+$url.git#tag=v$pkgver?signed")
validpgpkeys=(
    "4DAA67A9EA8B91FCC15B699C85CDAE3C164BA7B4"  # Christian DÃ¼rr <contact@christianduerr.com>
)
sha256sums=("112fc58e559094a0fbbddd88caf46254e31f2164e9d47f9cac9f08a2c1523009")

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"

  CARGO_INCREMENTAL=0 cargo build --release --locked --offline
}

check() {
  cd "$pkgname"

  CARGO_INCREMENTAL=0 cargo test --locked --offline
}

package() {
  cd "$pkgname"
  desktop-file-install -m 644 --dir "$pkgdir/usr/share/applications/" "charon.desktop"
  install -D -m755 "./target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -D -m644 "logo.svg" "$pkgdir/usr/share/pixmaps/Charon.svg"
}
