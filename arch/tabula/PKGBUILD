pkgname=tabula
pkgdesc="Wayland Wallpaper Tool"
pkgver=1.0.1
pkgrel=1
arch=("x86_64" "aarch64")
url="https://github.com/catacombing/tabula"
license=("GPL3")
makedepends=("git" "rust" "cargo")
source=("git+$url.git#tag=v$pkgver?signed")
validpgpkeys=(
    "4DAA67A9EA8B91FCC15B699C85CDAE3C164BA7B4"  # Christian DÃ¼rr <contact@christianduerr.com>
)
sha256sums=("7e734e75fd5dd1ca23d6caa00954dbf7d762c45daa13235e2066e104ca52343c")
replaces=("tabula-git")

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"

  CARGO_INCREMENTAL=0 cargo build --release --locked --offline
}

check() {
  cd "$pkgname"

  CARGO_INCREMENTAL=0 cargo test --locked --offline
}

package() {
  install -D -m 755 "$pkgname/target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
}
