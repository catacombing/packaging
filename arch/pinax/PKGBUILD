pkgname=pinax
pkgdesc="Wayland mobile notepad"
pkgver=1.2.3
pkgrel=1
arch=("x86_64" "aarch64")
url="https://github.com/catacombing/pinax"
license=("GPL3")
makedepends=("git" "rust" "cargo")
source=("git+$url.git#tag=v$pkgver?signed")
validpgpkeys=(
    "4DAA67A9EA8B91FCC15B699C85CDAE3C164BA7B4"  # Christian DÃ¼rr <contact@christianduerr.com>
)
sha256sums=("899bf4863ee66608203ca44e2692fd290c22f0b9a4e197f01f6aef03ecd43bee")

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"

  CARGO_INCREMENTAL=0 cargo build --release --locked --offline
}

check() {
  cd "$pkgname"

  CARGO_INCREMENTAL=0 cargo test --locked --offline
}

package() {
  cd "$pkgname"
  desktop-file-install -m 644 --dir "$pkgdir/usr/share/applications/" "pinax.desktop"
  install -D -m755 "./target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -D -m644 "logo.svg" "$pkgdir/usr/share/pixmaps/Pinax.svg"
}
